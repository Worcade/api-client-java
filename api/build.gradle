// Copyright (c) 2017, Worcade. Please see the AUTHORS file for details.
// All rights reserved. Use of this source code is governed by a MIT-style
// license that can be found in the LICENSE file.

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven'

dependencies {
    compileOnly 'org.projectlombok:lombok:1.16.18'

    compile ('com.google.guava:guava:23.0') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
        exclude group: 'com.google.j2objc', module: 'j2objc-annotations'
        exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
    }

    shadow 'org.slf4j:slf4j-api:1.7.25'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

javadoc {
    options.with {
        links 'https://docs.oracle.com/javase/8/docs/api/'
    }
}

shadowJar {
    baseName = 'api-client'
    classifier = ''

    relocate 'com.google', 'net.worcade.repackaged.com.google'
}

uploadShadow {
    repositories {
        mavenDeployer {
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: ossrhUser, password: ossrhPassword)
            }
        }
    }
}
